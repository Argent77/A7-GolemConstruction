// *** Type-specific actions (Maggot Golem) ***

// Work-around to ensure golem is properly equipped after being released from Imprisonment
IF
  Die()
  !StateCheck(Myself, STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    Continue()
END

IF
  Die()
  StateCheck(Myself, A7_STATE_IMMEDIATE_DEATH)  // STATE_EXPLODING_DEATH | STATE_FLAME_DEATH | STATE_ACID_DEATH | STATE_DEAD
  GlobalLT("Command", "LOCALS", 2)
THEN
  RESPONSE #100
    IncrementGlobal("A7!GolemCount", "GLOBAL", -1)
END

IF
  Global("initTimer", "LOCALS", 0)
THEN
  RESPONSE #25
    SetGlobal("initTimer", "LOCALS", 1)
    SetGlobalTimer("durationTimer", "LOCALS", SEVEN_DAYS)
  RESPONSE #25
    SetGlobal("initTimer", "LOCALS", 1)
    SetGlobalTimer("durationTimer", "LOCALS", EIGHT_DAYS)
  RESPONSE #25
    SetGlobal("initTimer", "LOCALS", 1)
    SetGlobalTimer("durationTimer", "LOCALS", NINE_DAYS)
  RESPONSE #25
    SetGlobal("initTimer", "LOCALS", 1)
    SetGlobalTimer("durationTimer", "LOCALS", TEN_DAYS)
END

IF
  Global("dismiss", "LOCALS", 2)
  GlobalTimerExpired("durationTimer", "LOCALS")
THEN
  RESPONSE #100
  SetGlobal("dismiss", "LOCALS", 3)
  SetInterrupt(FALSE)
  Kill(Myself)
  SetInterrupt(TRUE)
END

IF
  GlobalLT("dismiss", "LOCALS", 2)
  GlobalTimerExpired("durationTimer", "LOCALS")
THEN
  RESPONSE #50
    SetGlobal("dismiss", "LOCALS", 3)
    SetInterrupt(FALSE)
    DisplayString(Myself, @7003) // The golem's substance has been wholly consumed by the maggots and loses its coherence...
    Kill(Myself)
    SetInterrupt(TRUE)
  RESPONSE #13
    SetGlobal("dismiss", "LOCALS", 2)
    SetGlobal("initTimer", "LOCALS", 2)
    SetInterrupt(FALSE)
    SetGlobalTimer("durationTimer", "LOCALS", ONE_ROUND)
    DisplayString(Myself, @7004) // The golem's substance has been wholly consumed by the maggots. The increasing frenzy of the insects...
    Enemy()
    SetInterrupt(TRUE)
  RESPONSE #13
    SetGlobal("dismiss", "LOCALS", 2)
    SetGlobal("initTimer", "LOCALS", 2)
    SetInterrupt(FALSE)
    SetGlobalTimer("durationTimer", "LOCALS", TWO_ROUNDS)
    DisplayString(Myself, @7004) // The golem's substance has been wholly consumed by the maggots. The increasing frenzy of the insects...
    Enemy()
    SetInterrupt(TRUE)
  RESPONSE #12
    SetGlobal("dismiss", "LOCALS", 2)
    SetGlobal("initTimer", "LOCALS", 2)
    SetInterrupt(FALSE)
    SetGlobalTimer("durationTimer", "LOCALS", THREE_ROUNDS)
    DisplayString(Myself, @7004) // The golem's substance has been wholly consumed by the maggots. The increasing frenzy of the insects...
    Enemy()
    SetInterrupt(TRUE)
  RESPONSE #12
    SetGlobal("dismiss", "LOCALS", 2)
    SetGlobal("initTimer", "LOCALS", 2)
    SetInterrupt(FALSE)
    SetGlobalTimer("durationTimer", "LOCALS", FOUR_ROUNDS)
    DisplayString(Myself, @7004) // The golem's substance has been wholly consumed by the maggots. The increasing frenzy of the insects...
    Enemy()
    SetInterrupt(TRUE)
END

// Work-around for issue: Fix creature specifications
IF
  !Specifics(Myself, A7!GOLEM)
THEN
  RESPONSE #100
    ChangeGeneral(Myself, GIANTHUMANOID)
    ChangeRace(Myself, GOLEM)
    ChangeClass(Myself, GOLEM_FLESH)
    ChangeSpecifics(Myself, A7!GOLEM)
    ChangeGender(Myself, NIETHER)
END
