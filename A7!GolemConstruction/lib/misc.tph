// *** Misc. stuff not fitting elsewhere ***

ACTION_IF (GAME_IS ~bgee eet~) BEGIN
  // Renting second floor of Sorcerous Sundries for building golems
  COMPILE ~%MOD_FOLDER%/dialogs/bdhalbaz.d~

  // Use Coldhearth dungeon as golem building workshop
  EXTEND_TOP ~bd1200.bcs~ ~%MOD_FOLDER%/scripts/bd1200_workshop.baf~
  COMPILE ~%MOD_FOLDER%/scripts/a7!bd12a.baf~
          ~%MOD_FOLDER%/scripts/a7!bd12b.baf~
  COPY_EXISTING ~bd1200.are~ ~override~
    LPF fj_are_structure
    INT_VAR
      fj_type           = 0       // proximiy trigger
      fj_box_left       = 1500
      fj_box_top        = 1030
      fj_box_right      = 1510
      fj_box_bottom     = 1040
      fj_cursor_idx     = 0       // default cursor
      fj_flags          = (1 << 1)  // [Trap resets]
      fj_trap_active    = 1         // is trapped
      fj_vertex_0       = 1505 | (1040 << 16)
      fj_vertex_1       = 1500 | (1030 << 16)
      fj_vertex_2       = 1510 | (1030 << 16)
    STR_VAR
      fj_structure_type = "region"
      fj_name           = "A7!EnemyCheck1"
      fj_reg_script     = "a7!bd12a"
    END
    LPF fj_are_structure
    INT_VAR
      fj_type           = 0       // proximiy trigger
      fj_box_left       = 2010
      fj_box_top        = 1300
      fj_box_right      = 2020
      fj_box_bottom     = 1310
      fj_cursor_idx     = 0       // default cursor
      fj_flags          = (1 << 1)  // [Trap resets]
      fj_trap_active    = 1         // is trapped
      fj_vertex_0       = 2015 | (1310 << 16)
      fj_vertex_1       = 2010 | (1300 << 16)
      fj_vertex_2       = 2020 | (1300 << 16)
    STR_VAR
      fj_structure_type = "region"
      fj_name           = "A7!EnemyCheck2"
      fj_reg_script     = "a7!bd12b"
    END

  // Use Ruins of Kanaglym as golem building workshop
  EXTEND_TOP ~bd5300.bcs~ ~%MOD_FOLDER%/scripts/bd5300_workshop.baf~
  COMPILE ~%MOD_FOLDER%/scripts/a7!bd53a.baf~
          ~%MOD_FOLDER%/scripts/a7!bd53b.baf~
          ~%MOD_FOLDER%/scripts/a7!bd53c.baf~
  COPY_EXISTING ~bd5300.are~ ~override~
    LPF fj_are_structure
    INT_VAR
      fj_type           = 0       // proximiy trigger
      fj_box_left       = 1650
      fj_box_top        = 580
      fj_box_right      = 1660
      fj_box_bottom     = 590
      fj_cursor_idx     = 0       // default cursor
      fj_flags          = (1 << 1)  // [Trap resets]
      fj_trap_active    = 1         // is trapped
      fj_vertex_0       = 1655 | (590 << 16)
      fj_vertex_1       = 1650 | (580 << 16)
      fj_vertex_2       = 1660 | (580 << 16)
    STR_VAR
      fj_structure_type = "region"
      fj_name           = "A7!EnemyCheck1"
      fj_reg_script     = "a7!bd53a"
    END
    LPF fj_are_structure
    INT_VAR
      fj_type           = 0       // proximiy trigger
      fj_box_left       = 670
      fj_box_top        = 1080
      fj_box_right      = 680
      fj_box_bottom     = 1090
      fj_cursor_idx     = 0       // default cursor
      fj_flags          = (1 << 1)  // [Trap resets]
      fj_trap_active    = 1         // is trapped
      fj_vertex_0       = 675 | (1090 << 16)
      fj_vertex_1       = 670 | (1080 << 16)
      fj_vertex_2       = 680 | (1080 << 16)
    STR_VAR
      fj_structure_type = "region"
      fj_name           = "A7!EnemyCheck2"
      fj_reg_script     = "a7!bd53b"
    END
    LPF fj_are_structure
    INT_VAR
      fj_type           = 0       // proximiy trigger
      fj_box_left       = 1250
      fj_box_top        = 1070
      fj_box_right      = 1260
      fj_box_bottom     = 1080
      fj_cursor_idx     = 0       // default cursor
      fj_flags          = (1 << 1)  // [Trap resets]
      fj_trap_active    = 1         // is trapped
      fj_vertex_0       = 1255 | (1080 << 16)
      fj_vertex_1       = 1250 | (1070 << 16)
      fj_vertex_2       = 1260 | (1070 << 16)
    STR_VAR
      fj_structure_type = "region"
      fj_name           = "A7!EnemyCheck3"
      fj_reg_script     = "a7!bd53c"
    END
END

ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
  // importing items into starting dungeon (one tome and one ingredient)
  COPY_EXISTING ~ar0602.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE
        ~\(ActionOverride("Shelf1",TakeItemListPartyNum(.+))\)~
        ~\1 ActionOverride("Shelf17",TakeItemListPartyNum("A7!IMPT1",1)) ActionOverride("Crate2",TakeItemListPartyNum("A7!IMPT2",1)) ActionOverride("Shelf6",TakeItemListPartyNum("A7!IMPT3",4)) ActionOverride("Drawer1",TakeItemListPartyNum("A7!IMPT4",1))~
    END
    BUT_ONLY

  COPY ~%MOD_FOLDER%/items/a7!impt1.2da~ ~override~
       ~%MOD_FOLDER%/items/a7!impt2.2da~ ~override~
       ~%MOD_FOLDER%/items/a7!impt3.2da~ ~override~
       ~%MOD_FOLDER%/items/a7!impt4.2da~ ~override~

  // Use Planar Sphere as golem building workshop
  EXTEND_BOTTOM ~ar0411.bcs~ ~%MOD_FOLDER%/scripts/ar0411_bottom.baf~

  // Use Jermien's home in Imnesville as golem building workshop
  COMPILE ~%MOD_FOLDER%/dialogs/jermien.d~
  COPY_EXISTING ~ar1103.are~ ~override~
    READ_ASCII AREA_SCRIPT areaScript (8) NULL
    PATCH_IF (FILE_EXISTS_IN_GAME ~%areaScript%.bcs~) BEGIN
      // appending script
      INNER_ACTION BEGIN EXTEND_BOTTOM ~%areaScript%.bcs~ ~%MOD_FOLDER%/scripts/ar1103.baf~ END
    END ELSE BEGIN
      // assigning new script
      INNER_ACTION BEGIN COMPILE ~%MOD_FOLDER%/scripts/ar1103.baf~ END
      WRITE_ASCII AREA_SCRIPT ~AR1103~ #8
    END
  BUT_ONLY

  // Use Watcher's Keep - Second Level as golem workshop
  EXTEND_TOP ~ar3016.bcs~ ~%MOD_FOLDER%/scripts/ar3016_top.baf~

  // Use Pocket Plane as golem workshop
  COMPILE ~%MOD_FOLDER%/dialogs/25spell.d~
  EXTEND_TOP ~ar4500.bcs~ ~%MOD_FOLDER%/scripts/ar4500_top.baf~
END
