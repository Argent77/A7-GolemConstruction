// *** Generic stuff that has to be defined (first) ***

// SPECIFICS value for all constructed golem types
OUTER_SET specific_golem  = "-1"
OUTER_FOR (id = 200; id < 256; id += 1) BEGIN
  // try to find a free slot first
  OUTER_PATCH ~~ BEGIN
    LOOKUP_IDS_SYMBOL_OF_INT name ~specific~ id
  END
  ACTION_IF (~%name%~ STRING_EQUAL ~%id%~) BEGIN
    OUTER_SET specific_golem  = id
    OUTER_SET id = 256
  END
END
ACTION_IF (specific_golem < 0) BEGIN
  // default to SPECIFIC value = 200
  OUTER_SET specific_golem  = 200
END
APPEND ~specific.ids~ ~%specific_golem% A7!GOLEM~

// SPLSTATE values used for targeting specific golem types or variants
APPEND ~splstate.ids~ ~167 A7!GOLEM_LESSER~ UNLESS ~A7!GOLEM_LESSER~
APPEND ~splstate.ids~ ~168 A7!GOLEM_NORMAL~ UNLESS ~A7!GOLEM_NORMAL~
APPEND ~splstate.ids~ ~169 A7!GOLEM_GREATER~ UNLESS ~A7!GOLEM_GREATER~
APPEND ~splstate.ids~ ~170 A7!GOLEM_PERFECT~ UNLESS ~A7!GOLEM_PERFECT~
APPEND ~splstate.ids~ ~171 A7!GOLEM_FLESH~ UNLESS ~A7!GOLEM_FLESH~
APPEND ~splstate.ids~ ~172 A7!GOLEM_CLAY~ UNLESS ~A7!GOLEM_CLAY~
APPEND ~splstate.ids~ ~173 A7!GOLEM_STONE~ UNLESS ~A7!GOLEM_STONE~
APPEND ~splstate.ids~ ~174 A7!GOLEM_IRON~ UNLESS ~A7!GOLEM_IRON~
APPEND ~splstate.ids~ ~175 A7!GOLEM_MITHRAL~ UNLESS ~A7!GOLEM_MITHRAL~
APPEND ~splstate.ids~ ~178 A7!GOLEM_BONE~ UNLESS ~A7!GOLEM_BONE~
APPEND ~splstate.ids~ ~180 A7!GOLEM_ICE~ UNLESS ~A7!GOLEM_ICE~
APPEND ~splstate.ids~ ~182 A7!GOLEM_LIGHTNING~ UNLESS ~A7!GOLEM_LIGHTNING~
APPEND ~splstate.ids~ ~183 A7!GOLEM_RUBY~ UNLESS ~A7!GOLEM_RUBY~
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  APPEND ~splstate.ids~ ~184 A7!GOLEM_FIGHTER~ UNLESS ~A7!GOLEM_FIGHTER~  // Used for Animated Plates and similar constructs
END ELSE BEGIN
  APPEND ~splstate.ids~ ~176 A7!GOLEM_ADAMANTITE~ UNLESS ~A7!GOLEM_ADAMANTITE~
  APPEND ~splstate.ids~ ~177 A7!GOLEM_MAGGOT~ UNLESS ~A7!GOLEM_MAGGOT~
  APPEND ~splstate.ids~ ~179 A7!GOLEM_BRAIN~ UNLESS ~A7!GOLEM_BRAIN~
  APPEND ~splstate.ids~ ~181 A7!GOLEM_MAGIC~ UNLESS ~A7!GOLEM_MAGIC~
END

// (max. number - 1) of available golems following you
OUTER_SET max_golem_count = 2

// probably not needed
APPEND ~gtimes.ids~ ~6 ONE_ROUND~ UNLESS ~ONE_ROUND~
APPEND ~gtimes.ids~ ~12 TWO_ROUNDS~ UNLESS ~TWO_ROUNDS~
APPEND ~gtimes.ids~ ~18 THREE_ROUNDS~ UNLESS ~THREE_ROUNDS~
APPEND ~gtimes.ids~ ~24 FOUR_ROUNDS~ UNLESS ~FOUR_ROUNDS~
APPEND ~gtimes.ids~ ~30 FIVE_ROUNDS~ UNLESS ~FIVE_ROUNDS~
APPEND ~gtimes.ids~ ~36000 FIVE_DAYS~ UNLESS ~FIVE_DAYS~
APPEND ~gtimes.ids~ ~50400 SEVEN_DAYS~ UNLESS ~SEVEN_DAYS~
APPEND ~gtimes.ids~ ~57600 EIGHT_DAYS~ UNLESS ~EIGHT_DAYS~
APPEND ~gtimes.ids~ ~64800 NINE_DAYS~ UNLESS ~NINE_DAYS~
APPEND ~gtimes.ids~ ~72000 TEN_DAYS~ UNLESS ~TEN_DAYS~

APPEND ~state.ids~ ~0x00000F00 A7_STATE_IMMEDIATE_DEATH~ UNLESS ~A7_STATE_IMMEDIATE_DEATH~

// Golem abilities are ported over to BG2EE
ACTION_IF (GAME_IS ~bg2ee~) BEGIN
  COPY_EXISTING - ~varimprt.2da~ ~override~
    COUNT_2DA_COLS numCols
    COUNT_2DA_ROWS numCols numRows

  ACTION_DEFINE_ARRAY vars BEGIN
    ~A7!TOMEGOLEM~ 
    ~A7!TOMEGOLEMFLESH~
    ~A7!TOMEGOLEMCLAY~
    ~A7!TOMEGOLEMSTONE~
    ~A7!TOMEGOLEMIRON~
    ~A7!TOMEGOLEMMITHRAL~
    ~A7!TOMEGOLEMADAMANTITE~
    ~A7!TOMEGOLEMMAGGOT~
    ~A7!TOMEGOLEMBONE~
    ~A7!TOMEGOLEMBRAIN~
    ~A7!TOMEGOLEMICE~
    ~A7!TOMEGOLEMMAGIC~
    ~A7!TOMEGOLEMLIGHTNING~
    ~A7!TOMEGOLEMRUBY~
  END
  OUTER_SET varIndex = numRows
  ACTION_PHP_EACH vars AS _ => var BEGIN
    APPEND ~varimprt.2da~ ~%varIndex% %var%~
    OUTER_SET varIndex += 1
  END
END


// check for SPLPROT entry: SPECIFIC != value
OUTER_SET splprot_not_specific = 116  // this entry is used by v2.x patched games
LAF ADD_SPLPROT_ENTRY
INT_VAR
  stat      = 0x10e
STR_VAR
  value     = "-1"
  relation  = "5"
RET
  index
END
ACTION_IF (index >= 0) BEGIN
  OUTER_SET splprot_not_specific = index
END


// Grouping relevant journal entries
ACTION_IF (GAME_IS ~bgee~) BEGIN
  ADD_JOURNAL @49001 @49002 @49008
END ELSE ACTION_IF (GAME_IS ~bg2ee~) BEGIN
  ADD_JOURNAL @49003 @49004 @49005 @49006 @49007
END ELSE ACTION_IF (GAME_IS ~eet~) BEGIN
  ADD_JOURNAL @49001 @49002 @49003 @49004 @49005 @49006 @49007 @49008
END ELSE ACTION_IF (GAME_IS ~iwdee~) BEGIN
  ADD_JOURNAL @140000 @140001 @140002 @140003 @140004 @140005 @140006 @140007
END
